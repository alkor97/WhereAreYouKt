
static String[] getVersionTags() {
    def prefix = 'v'

    def process = ['git', 'tag', '-l', '--sort', '-refname', "${prefix}*"].execute()
    process.err.eachLine {line -> println 'ERROR: ' + line}
    process.waitFor()

    def output = process.text.trim()
    return output.split("\n")
}

static boolean hasNotCommittedFiles() {
    def process = ['git', 'status', '-s'].execute()
    process.err.eachLine {line -> println 'ERROR: ' + line}
    process.waitFor()

    return !process.text.trim().isEmpty()
}

static String hashOf(String tag) {
    def process = ['git', 'rev-list', '-n', '1', tag].execute()
    process.err.eachLine {line -> println 'ERROR: ' + line}
    process.waitFor()

    return process.text.trim()
}

static String getCommitMessagesBetween(String from, String until = "HEAD") {
    def process = ['git', 'log', "${from}..${until}", "--oneline"].execute()
    process.err.eachLine {line -> println 'ERROR: ' + line}
    process.waitFor()

    return process.text.trim()
}

def versionTags = getVersionTags()
def tag = versionTags[0]
def version = tag.substring(1).split('\\.')

def dirty = ''
if (hasNotCommittedFiles()) {
    dirty = '+'
    project.logger.lifecycle("Non-committed files exist in repository.")
} else {
    def hashOfHead = hashOf('HEAD')
    def hashOfTag = hashOf("tags/$tag")
    if (hashOfHead != hashOfTag) {
        dirty = '+'
        project.logger.lifecycle("\nHEAD does not match $tag. Consider creating new tag representing bumped version.")
    }
}

def NAMES = ["major", "minor", "patch"]
def MAX_VALUES = [99, 99, 99]
def code = 0

for (int i = 0; i < version.length; ++i) {
    def component = version[i].toInteger()
    def name = NAMES[i]
    if (component > MAX_VALUES[i])
        throw new GradleException("${name} version ${component} exceeds limit")
    def multiplier = MAX_VALUES.subList(i + 1, version.length).inject(1, {result, entry -> result * (entry + 1) })
    code += component * multiplier
}

def name = version.join('.') + dirty
project.logger.lifecycle("\nComputed application version is $name")

android {
    defaultConfig {
        versionCode code
        versionName name
    }
}

def until = 'HEAD'
if (dirty.isEmpty()) {
    until = versionTags[1]
}
project.logger.lifecycle("\nCommits between ${tag} and ${until}:\n" + getCommitMessagesBetween(tag, until))
